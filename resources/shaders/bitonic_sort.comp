#version 460 core
layout (local_size_x = 512, local_size_y = 1, local_size_z = 1) in;

struct VisibleTransparentObject {
    uint objectId;
    float distance;
};

layout(binding = 1, std430) buffer VisibleTransparentObjectBuffer {
    VisibleTransparentObject visibleObjects[];
};

layout(std140, binding = 0) uniform SortConstants {
    uint k;
    uint j;
    uint padding0;
    uint padding1;
} constants;

void main() {
    uint i = gl_GlobalInvocationID.x;

    uint ixj = i ^ constants.j;

    if (ixj > i) {
        if ((i & constants.k) == 0) {
            if (visibleObjects[i].distance > visibleObjects[ixj].distance) {
                VisibleTransparentObject temp = visibleObjects[i];
                visibleObjects[i] = visibleObjects[ixj];
                visibleObjects[ixj] = temp;
            }
        } else {
            if (visibleObjects[i].distance < visibleObjects[ixj].distance) {
                VisibleTransparentObject temp = visibleObjects[i];
                visibleObjects[i] = visibleObjects[ixj];
                visibleObjects[ixj] = temp;
            }
        }
    }
}
