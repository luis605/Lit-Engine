#version 460 core
layout (local_size_x = 512, local_size_y = 1, local_size_z = 1) in;

struct RenderableComponent {
    uint mesh_uuid;
    uint material_uuid;
    uint shaderId;
    uint objectId;
    float alpha;
};

layout(std430) buffer VisibleLargeObjectBuffer {
    uint visibleLargeObjects[];
};

layout(std430) readonly buffer RenderableBuffer {
    RenderableComponent renderables[];
};

layout(std140) uniform SortConstants {
    uint u_sort_k;
    uint u_sort_j;
};

bool is_less(uint a, uint b) {
    RenderableComponent r_a = renderables[a];
    RenderableComponent r_b = renderables[b];
    return r_a.mesh_uuid < r_b.mesh_uuid;
}

void main() {
    uint i = gl_GlobalInvocationID.x;

    uint ixj = i ^ u_sort_j;

    if (ixj > i) {
        if ((i & u_sort_k) == 0) {
            if (!is_less(visibleLargeObjects[i], visibleLargeObjects[ixj])) {
                uint temp = visibleLargeObjects[i];
                visibleLargeObjects[i] = visibleLargeObjects[ixj];
                visibleLargeObjects[ixj] = temp;
            }
        } else {
            if (is_less(visibleLargeObjects[i], visibleLargeObjects[ixj])) {
                uint temp = visibleLargeObjects[i];
                visibleLargeObjects[i] = visibleLargeObjects[ixj];
                visibleLargeObjects[ixj] = temp;
            }
        }
    }
}