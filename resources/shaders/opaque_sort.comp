#version 460 core
layout (local_size_x = 512, local_size_y = 1, local_size_z = 1) in;

struct RenderableComponent {
    uint mesh_uuid;
    uint material_uuid;
    uint shaderId;
    uint objectId;
    float alpha;
};

layout(binding = 1, std430) buffer VisibleObjectBuffer {
    uint visibleObjects[];
};

layout(binding = 4, std430) readonly buffer RenderableBuffer {
    RenderableComponent renderables[];
};

uniform uint u_sort_j;
uniform uint u_sort_k;

bool is_less(uint a, uint b) {
    RenderableComponent r_a = renderables[a];
    RenderableComponent r_b = renderables[b];
    if (r_a.shaderId < r_b.shaderId) return true;
    if (r_a.shaderId > r_b.shaderId) return false;
    return r_a.mesh_uuid < r_b.mesh_uuid;
}

void main() {
    uint i = gl_GlobalInvocationID.x;

    uint ixj = i ^ u_sort_j;

    if (ixj > i) {
        if ((i & u_sort_k) == 0) {
            if (!is_less(visibleObjects[i], visibleObjects[ixj])) {
                uint temp = visibleObjects[i];
                visibleObjects[i] = visibleObjects[ixj];
                visibleObjects[ixj] = temp;
            }
        } else {
            if (is_less(visibleObjects[i], visibleObjects[ixj])) {
                uint temp = visibleObjects[i];
                visibleObjects[i] = visibleObjects[ixj];
                visibleObjects[ixj] = temp;
            }
        }
    }
}
