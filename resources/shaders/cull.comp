#version 460 core

layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

struct DrawElementsIndirectCommand {
    uint count;
    uint instanceCount;
    uint firstIndex;
    uint baseVertex;
    uint baseInstance;
};

struct MeshInfo {
    uint indexCount;
    uint firstIndex;
    uint baseVertex;
    float boundingRadius;
    vec4 boundingCenter;
};

struct RenderableComponent {
    uint mesh_uuid;
    uint material_uuid;
    uint shaderId;
    uint objectId;
};

layout(binding = 0, std430) buffer AtomicCounterBuffer {
    uint drawCounts[];
};
layout(binding = 1, std430) buffer DrawCommandBuffer {
    DrawElementsIndirectCommand commands[];
};

struct TransformComponent {
    mat4 localMatrix;
    mat4 worldMatrix;
};

layout(binding = 2, std430) readonly buffer ObjectBuffer {
    TransformComponent transforms[];
};

layout(binding = 3, std430) readonly buffer MeshInfoBuffer {
    MeshInfo meshInfos[];
};

layout(binding = 4, std430) readonly buffer RenderableBuffer {
    RenderableComponent renderables[];
};

uniform uint u_objectCount;
uniform uint u_maxDraws;
uniform mat4 u_viewProjection;

bool isVisible(vec4 clip_pos, float radius) {
    return clip_pos.x >= -clip_pos.w - radius && clip_pos.x <= clip_pos.w + radius &&
           clip_pos.y >= -clip_pos.w - radius && clip_pos.y <= clip_pos.w + radius &&
           clip_pos.z >= -clip_pos.w - radius && clip_pos.z <= clip_pos.w + radius;
}

void main() {
    uint objectId = gl_GlobalInvocationID.x;
    if (objectId >= u_objectCount) return;

    RenderableComponent renderable = renderables[objectId];
    mat4 modelMatrix = transforms[objectId].worldMatrix;
    MeshInfo mesh = meshInfos[renderable.mesh_uuid];

    vec4 world_pos = modelMatrix * mesh.boundingCenter;
    vec4 clip_pos = u_viewProjection * world_pos;

    float world_radius = mesh.boundingRadius * max(length(modelMatrix[0]), max(length(modelMatrix[1]), length(modelMatrix[2])));

    if (isVisible(clip_pos, world_radius)) {
        uint shaderId = renderable.shaderId;
        uint indexInBin = atomicAdd(drawCounts[shaderId], 1);

        if (indexInBin < u_maxDraws) {
            uint writeIndex = shaderId * u_maxDraws + indexInBin;
            commands[writeIndex].instanceCount = 1;
            commands[writeIndex].baseInstance = objectId;
            commands[writeIndex].count = mesh.indexCount;
            commands[writeIndex].firstIndex = mesh.firstIndex;
            commands[writeIndex].baseVertex = mesh.baseVertex;
        }
    }
}
