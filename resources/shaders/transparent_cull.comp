#version 460 core

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

struct DrawElementsIndirectCommand {
    uint count;
    uint instanceCount;
    uint firstIndex;
    uint baseVertex;
    uint baseInstance;
};

struct MeshInfo {
    uint indexCount;
    uint firstIndex;
    uint baseVertex;
    float boundingRadius;
    vec4 boundingCenter;
};

struct RenderableComponent {
    uint mesh_uuid;
    uint material_uuid;
    uint shaderId;
    uint objectId;
    float alpha;
};

struct VisibleTransparentObject {
    uint objectId;
    float distance;
};

layout (std140, binding = 0) uniform SceneData {
    mat4 projection;
    mat4 view;
    vec3 lightPos;
    vec3 viewPos;
    vec3 lightColor;
    vec4 frustumPlanes[6];
} sceneData;

layout(binding = 0, std430) buffer AtomicCounterBuffer {
    uint visibleTransparentCount;
};
layout(binding = 1, std430) buffer VisibleTransparentObjectBuffer {
    VisibleTransparentObject visibleObjects[];
};

struct TransformComponent {
    mat4 localMatrix;
    mat4 worldMatrix;
};

layout(binding = 2, std430) readonly buffer ObjectBuffer {
    TransformComponent transforms[];
};
layout(binding = 3, std430) readonly buffer MeshInfoBuffer {
    MeshInfo meshInfos[];
};
layout(binding = 4, std430) readonly buffer RenderableBuffer {
    RenderableComponent renderables[];
};

layout(std140, binding = 1) uniform TransparentCullUniforms {
    uint objectCount;
    float padding0;
    float padding1;
    float padding2;
    vec3 cameraPos;
    float padding3;
} uniforms;

bool isVisible(vec3 world_pos, float radius) {
    for (int i = 0; i < 6; i++) {
        if (dot(sceneData.frustumPlanes[i].xyz, world_pos) + sceneData.frustumPlanes[i].w < -radius) {
            return false;
        }
    }
    return true;
}

void main() {
    uint objectId = gl_GlobalInvocationID.x;
    if (objectId >= uniforms.objectCount) return;

    RenderableComponent renderable = renderables[objectId];
    if (renderable.alpha == 1.0) return;

    mat4 modelMatrix = transforms[objectId].worldMatrix;
    MeshInfo mesh = meshInfos[renderable.mesh_uuid];

    vec4 world_pos_vec4 = modelMatrix * mesh.boundingCenter;
    vec3 world_pos = world_pos_vec4.xyz;

    float world_radius = mesh.boundingRadius * max(length(modelMatrix[0]), max(length(modelMatrix[1]), length(modelMatrix[2])));

    if (isVisible(world_pos, world_radius)) {
        uint index = atomicAdd(visibleTransparentCount, 1);
        visibleObjects[index].objectId = objectId;
        visibleObjects[index].distance = distance(world_pos.xyz, uniforms.cameraPos);
    }
}
