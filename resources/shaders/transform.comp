#version 460 core

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

struct TransformComponent {
    mat4 localMatrix;
    mat4 worldMatrix;
};

struct HierarchyComponent {
    uint parent;
    uint level;
};

layout(binding = 0, std430) buffer TransformBuffer {
    TransformComponent transforms[];
};

layout(binding = 1, std430) readonly buffer HierarchyBuffer {
    HierarchyComponent hierarchies[];
};

layout(binding = 2, std430) readonly buffer SortedHierarchyBuffer {
    uint sortedHierarchyList[];
};

layout(binding = 3, std140) uniform TransformUniforms {
    uint u_objectCount;
    uint u_currentHierarchyLevel;
    uint u_baseIndex;
    uint u_padding;
};

void main() {
    uint index = gl_GlobalInvocationID.x;
    if (index >= u_objectCount) return;

    uint sortedIndex = index + u_baseIndex;
    uint objectId = sortedHierarchyList[sortedIndex];
    uint physicalObjectId = objectId + u_baseIndex;

    HierarchyComponent hierarchy = hierarchies[physicalObjectId];

    if (hierarchy.level != u_currentHierarchyLevel) return;

    if (hierarchy.parent != 0xFFFFFFFF) { // INVALID_ENTITY
        uint physicalParentId = hierarchy.parent + u_baseIndex;
        transforms[physicalObjectId].worldMatrix = transforms[physicalParentId].worldMatrix * transforms[physicalObjectId].localMatrix;
    } else {
        transforms[physicalObjectId].worldMatrix = transforms[physicalObjectId].localMatrix;
    }
}
