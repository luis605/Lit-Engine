#version 460 core

layout (local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

struct DrawElementsIndirectCommand {
    uint count;
    uint instanceCount;
    uint firstIndex;
    uint baseVertex;
    uint baseInstance;
};

struct MeshInfo {
    uint indexCount;
    uint firstIndex;
    uint baseVertex;
    float boundingRadius;
    vec4 boundingCenter;
};

struct RenderableComponent {
    uint mesh_uuid;
    uint material_uuid;
    uint shaderId;
    uint objectId;
    float alpha;
};

layout(binding = 0, std430) buffer AtomicCounterBuffer {
    uint drawCount;
};

layout(binding = 1, std430) buffer DrawCommandBuffer {
    DrawElementsIndirectCommand commands[];
};

layout(binding = 3, std430) readonly buffer MeshInfoBuffer {
    MeshInfo meshInfos[];
};

layout(binding = 4, std430) readonly buffer RenderableBuffer {
    RenderableComponent renderables[];
};

layout(binding = 5, std430) readonly buffer VisibleLargeObjectBuffer {
    uint visibleLargeObjects[];
};

uniform uint u_visibleLargeObjectCount;
uniform uint u_maxDraws;

void main() {
    if (u_visibleLargeObjectCount == 0) {
        return;
    }

    uint currentObjectId = visibleLargeObjects[0];
    RenderableComponent currentRenderable = renderables[currentObjectId];
    uint currentMeshId = currentRenderable.mesh_uuid;
    uint currentInstanceCount = 1;
    uint baseInstance = 0;

    for (uint i = 1; i < u_visibleLargeObjectCount; ++i) {
        uint objectId = visibleLargeObjects[i];
        RenderableComponent renderable = renderables[objectId];
        uint meshId = renderable.mesh_uuid;

        if (meshId == currentMeshId) {
            currentInstanceCount++;
        } else {
            MeshInfo mesh = meshInfos[currentMeshId];
            uint indexInBin = atomicAdd(drawCount, 1);
            if (indexInBin < u_maxDraws) {
                commands[indexInBin].instanceCount = currentInstanceCount;
                commands[indexInBin].baseInstance = baseInstance;
                commands[indexInBin].count = mesh.indexCount;
                commands[indexInBin].firstIndex = mesh.firstIndex;
                commands[indexInBin].baseVertex = mesh.baseVertex;
            }

            currentMeshId = meshId;
            currentInstanceCount = 1;
            baseInstance = i;
        }
    }

    MeshInfo mesh = meshInfos[currentMeshId];
    uint indexInBin = atomicAdd(drawCount, 1);
    if (indexInBin < u_maxDraws) {
        commands[indexInBin].instanceCount = currentInstanceCount;
        commands[indexInBin].baseInstance = baseInstance;
        commands[indexInBin].count = mesh.indexCount;
        commands[indexInBin].firstIndex = mesh.firstIndex;
        commands[indexInBin].baseVertex = mesh.baseVertex;
    }
}
